plugins {
    id 'java'
    id 'maven-publish'
    id 'org.springframework.boot' version '2.1.7.RELEASE'
    id 'war'
}

repositories {
    mavenLocal()
    maven {
        url = 'https://repo.spring.io/plugins-release/'
    }

    maven {
        url = 'http://builds.archive.org:8080/maven2'
    }

    maven {
        url = 'https://repo.maven.apache.org/maven2'
    }
}

dependencies {
    // This artifact no longer exists in public repos and we don't seem to need it
    //compile 'com.sun.jmx:jmxri:1.2.1'
    implementation 'log4j:log4j:1.2.17'
    implementation 'org.quartz-scheduler:quartz:2.3.1'
    implementation 'org.webcurator:webcurator-core:3.0.0-SNAPSHOT'
    implementation 'org.netarchivesuite:heritrix3-wrapper:1.0.3'
    implementation 'joda-time:joda-time:2.10.5'
    implementation group: 'com.sun.xml.bind', name: 'jaxb-impl', version: '2.3.2'
    implementation group: 'com.sun.xml.bind', name: 'jaxb-core', version: '2.3.0.1'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.9.5'
}

group = 'org.webcurator'
version = '3.0.0-SNAPSHOT'
description = 'Harvest Agent - Heritrix 3.x'
sourceCompatibility = '1.8'

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from(sourceSets.main.allJava)
}

// just an alias
task install(dependsOn: ['bootJar', 'publishToMavenLocal'])

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
            artifact(sourcesJar)
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
